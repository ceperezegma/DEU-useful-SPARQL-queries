**********************************
list of EU data providers (data provider name, type of EU organisation, EU organisation name) and published datasets
**********************************

PREFIX data: <http://data.odw.tw/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX ns: <http://www.w3.org/ns/org#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX at: <http://publications.europa.eu/ontology/authority/>

select ?dataProvider ?dataProviderName ?corporateBodyClassification ?classificationName (count(?datasetURI) as ?numDatasets)
where {
    <http://data.europa.eu/88u/catalogue/european-union-open-data-portal> <http://purl.org/dc/terms/hasPart> ?catalogue.
    ?catalogue a dcat:Catalog;
        dcat:dataset ?datasetURI.    
    ?datasetURI dct:publisher ?publisher.
    ?publisher at:authority-code ?dataProvider;
        skos:prefLabel ?dataProviderName;
        ns:classification ?publisherClasification.    
    ?publisherClasification at:authority-code ?corporateBodyClassification;
        skos:prefLabel ?classificationName.        
    filter(lang(?classificationName)="en").
    filter(lang(?dataProviderName)="en").
}
order by desc (?numDatasets)



**********************************
list of EU datasets and datasets titles in English
**********************************

PREFIX data: <http://data.odw.tw/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX ns: <http://www.w3.org/ns/org#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX at: <http://publications.europa.eu/ontology/authority/>

select ?datasetURI ?datasetTitle
where {
	<http://data.europa.eu/88u/catalogue/european-union-open-data-portal> <http://purl.org/dc/terms/hasPart> ?catalogue.
	?catalogue a dcat:Catalog.
	?catalogue  dcat:dataset ?datasetURI.
	?datasetURI dct:title ?datasetTitle.
  	filter(lang(?datasetTitle) = 'en' || lang(?datasetTitle) = '').
}
order by ?datasetURI


**********************************
number of published EU datasets per data provider + additional information about data providers
**********************************

PREFIX data: <http://data.odw.tw/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX ns: <http://www.w3.org/ns/org#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX at: <http://publications.europa.eu/ontology/authority/>

select ?dataProvider ?dataProviderName ?corporateBodyClassification ?classificationName (count(?datasetURI) as ?numDatasets)
where {
    <http://data.europa.eu/88u/catalogue/european-union-open-data-portal> <http://purl.org/dc/terms/hasPart> ?catalogue.
    ?catalogue a dcat:Catalog.
  	?catalogue dcat:dataset ?datasetURI.
  	optional {
  		?datasetURI dct:publisher ?publisher.
    	?publisher at:authority-code ?dataProvider.
    	?publisher skos:prefLabel ?dataProviderName.
    	?publisher ns:classification ?publisherClasification.
    	?publisherClasification at:authority-code ?corporateBodyClassification.
    	?publisherClasification skos:prefLabel ?classificationName.    	
    	filter(lang(?dataProviderName)="en").
    	filter(lang(?classificationName)="en").
  	}  
}
order by desc (?numDatasets)


**********************************
EU datasets that are not real datasets
**********************************

PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX odp:  <http://data.europa.eu/euodp/ontologies/ec-odp#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

select DISTINCT *
where {
	<http://data.europa.eu/88u/catalogue/european-union-open-data-portal> <http://purl.org/dc/terms/hasPart> ?catalog.
	?catalog dcat:dataset ?dataset.
	FILTER NOT EXISTS {?dataset a dcat:Dataset}
}


**********************************
list of EU datasets via the catalog record
**********************************

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

select DISTINCT *
where {
	<http://data.europa.eu/88u/catalogue/european-union-open-data-portal> <http://purl.org/dc/terms/hasPart> ?catalog.
	?catalog a dcat:Catalog.
  	?catalog dcat:record ?record.
  	?record foaf:primaryTopic ?dataset.
  	FILTER NOT EXISTS {?record a dcat:CatalogRecord}.
}


PREFIX ct: <http://data.linkedct.org/resource/linkedct/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX at: <http://publications.europa.eu/ontology/authority/>

select distinct *
where {
	<http://data.europa.eu/88u/catalogue/european-union-open-data-portal> <http://purl.org/dc/terms/hasPart> ?catalog.
	?catalog dcat:record ?record.
  	?catalog dcat:dataset ?dataset.
  	?record a dcat:CatalogRecord.
  	?record foaf:primaryTopic ?dataset.
  	?dataset a dcat:Dataset
}



**********************************
list of EU datsets without title in English
**********************************

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>

select ?datasetURI as ?DatasetWithoutEnTitle 
where {
		#<http://data.europa.eu/88u/catalogue/european-union-open-data-portal> <http://purl.org/dc/terms/hasPart> ?catalogue.
        ?catalogue a dcat:Catalog.
        ?catalogue  dcat:dataset ?datasetURI.
		filter not exists  {
                ?datasetURI dct:title ?datasetTitle.
                filter(lang(?datasetTitle) = 'en' || lang(?datasetTitle)= '').
	}
}


**********************************
extract for a EU dataset their publication and update dates and its related catalogRecord publication and update dates
**********************************

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

select distinct ?dataset ?publicationDate ?lastUpdateDate ?recordCreated ?recordUpdate
where {
	<http://data.europa.eu/88u/catalogue/european-union-open-data-portal> <http://purl.org/dc/terms/hasPart> ?catalogue.
	?catalog dcat:record ?record.  	
  	?record a dcat:CatalogRecord.
  	?record dct:issued ?recordCreated.  
  	?record dct:modified ?recordUpdate.
  	?record foaf:primaryTopic ?dataset.
  	?dataset a dcat:Dataset.
  	?dataset dct:issued ?publicationDate.
  	?dataset dct:modified ?lastUpdateDate.
  	filter(?dataset = <http://data.europa.eu/88u/dataset/consolidated-list-of-persons-groups-and-entities-subject-to-eu-financial-sanctions-fisma>)
  
}
order by desc(?lastUpdateDate)


**********************************
list of datasets for a specific data provider with publication and update dates and update frecuency
**********************************

PREFIX data: <http://data.odw.tw/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX ns: <http://www.w3.org/ns/org#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX at: <http://publications.europa.eu/ontology/authority/>

select ?datasetURI ?datasetPublicationDate ?datasetLastModificationDate ?updateFrequency
where {
    <http://data.europa.eu/88u/catalogue/european-union-open-data-portal> <http://purl.org/dc/terms/hasPart> ?catalogue.
    ?catalogue a dcat:Catalog;
        dcat:dataset ?datasetURI.
  	?datasetURI dct:publisher <http://publications.europa.eu/resource/authority/corporate-body/ECDC>. 
  	optional {?datasetURI dct:issued ?datasetPublicationDate}
    optional {?datasetURI dct:modified ?datasetLastModificationDate}
    optional {?datasetURI dct:accrualPeriodicity ?updateFrequency}
}
order by desc (?datasetPublicationDate)


**********************************
list of datasets for a specific data provider with publication and update dates and update frecuency for datasets and publication and update dates for the metadata (records)
**********************************

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX data: <http://data.odw.tw/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX ns: <http://www.w3.org/ns/org#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX at: <http://publications.europa.eu/ontology/authority/>

select distinct ?datasetURI ?updateFrequency ?datasetPublicationDate ?datasetLastModificationDate ?recordCreationDate ?recordLastModificationDate
where {
    <http://data.europa.eu/88u/catalogue/european-union-open-data-portal> <http://purl.org/dc/terms/hasPart> ?catalogue.
    ?catalogue a dcat:Catalog;
        dcat:dataset ?datasetURI.
  	?catalogueRecord foaf:primaryTopic ?datasetURI;
        dct:issued ?recordCreationDate;
		dct:modified ?recordLastModificationDate.
	?datasetURI dct:publisher <http://publications.europa.eu/resource/authority/corporate-body/ECDC>.
  	optional { ?datasetURI dct:issued ?datasetPublicationDate}
  	optional {?datasetURI dct:modified ?datasetLastModificationDate}
  	optional {?datasetURI dct:accrualPeriodicity ?updateFrequency}
}
order by desc (?datasetPublicationDate)


**********************************
count the number of datasets in the portal without including the datasets from the legacy ODP
**********************************

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>

select (count(?datasetURI) as ?numDatasets)
where {    
    ?catalogue a dcat:Catalog;
  		dcat:dataset ?datasetURI.
    filter not exists {?catalogue dct:isPartOf ?ODPCatalogue}.  	
}


**********************************
count the number of datasets in the portal per catalogue excluding EU catalogues
**********************************

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>

select ?country (count(?dataset) as ?numDatasets)
where {    
    ?catalogue a dcat:Catalog;  		
    	dct:spatial ?country;
     	dcat:dataset ?dataset.
  	filter(?country != <http://publications.europa.eu/resource/authority/continent/EUROPE>).  		
}
group by ?country
order by desc (?numDatasets)



**********************************
calculate the total size of datasets distributions
**********************************

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>

select (sum(?distributionSize) as ?totalDistributionsSize)
where {    
    ?catalogue a dcat:Catalog;    
     	dcat:dataset ?dataset.
  	?dataset dcat:distribution ?distribution.
  	?distribution dcat:byteSize ?distributionSize.  	 	
}


**********************************
datasets, distributions, download URL and distribution size in Bytes
**********************************

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>

select ?dataset ?format ?downloadURL ?distributionSize
where {    
    ?catalogue a dcat:Catalog;    
     	dcat:dataset ?dataset.
  	?dataset dcat:distribution ?distribution.
  	?distribution dct:format ?format;
             dcat:downloadURL ?downloadURL;
             dcat:byteSize ?distributionSize.
  	filter(?format = <http://publications.europa.eu/resource/authority/file-type/CSV>).  	
}
order by ?dataset
limit 100


**********************************
datasets created after a specific date: this date corresponds to the harvesting date and not the real date of datasets creation
**********************************

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
select distinct ?catalogue (count( ?dataset) as ?newDatasets)
where {
    ?catalogue a dcat:Catalog;
          dcat:dataset ?dataset.
	OPTIONAL {?dataset dct:issued ?publicationDateDataset}.
	?record a dcat:CatalogRecord;
		foaf:primaryTopic ?dataset;
		dct:issued ?publicationDateRecord.
	filter(?publicationDateRecord> "2022-07-29"^^xsd:date)
}  
ORDER BY DESC (?newDatasets)



**********************************
detect datasets present in more than 1 catalogue
**********************************

PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>

select distinct ?dataset 
where {
	?catalogue1 a dcat:Catalog;
  		dcat:dataset ?dataset.
  	?catalogue2 a dcat:Catalog;
    	dcat:dataset ?dataset. 
	filter(?catalogue1 != ?catalogue2 )
}


**********************************
detect EU datasets without data provider
**********************************

PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>

select distinct *
where {
   ?parentCategoy dct:hasPart ?category. 
   ?category dcat:dataset ?dataset.
   filter not exists {?dataset a dcat:Dataset}
   optional {?dataset dct:publisher ?publisher}
 }
limit 100